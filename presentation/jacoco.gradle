apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.5"
}

task fullCoverageReport(type: JacocoReport) {
    dependsOn(
            ":presentation:testDevelopDebugUnitTest",
            ":presentation:createDevelopDebugCoverageReport"
    )
    reports {
        csv.enabled = false
        xml.enabled = true
        html {
            enabled = true
            destination(file("${buildDir}/reports/coverage/develop/debug"))
        }
    }

    def fileFilter = [
            '**/*App.*',
            '**/*Application.*',
            '**/*Activity.*',
            '**/*Fragment.*',
            '**/*JsonAdapter.*', // adapters generated by Moshi
            '**/di/**',
            '**/*Dagger.*'
    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/developDebug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDevelopDebugUnitTest.exec",
            "outputs/code-coverage/developDebugAndroidTest/connected/*coverage.ec"
    ]))
}

project.tasks.whenTaskAdded {
    if (it.name == "testProductionReleaseUnitTest") {
        it.enabled = false
    }
}